{
  lib,
  config,
  ...
}:
{
  flake.lib = {
    # Helper to support namespacing with dot (`.`) in `flake.packages`
    # (eg. `nix build .#pkgs.${packageName}`).
    # This relies on the Nix completion not quoting attrset keys containing
    # a dot.
    # `derivations` may nest: an attribute holding an attribute set of
    # derivations is a scope, and is namespaced the same way one level down
    # (eg. `nix build .#pkgs.ocamlPackages.h3`).
    flakePackagesWithNamespace =
      { namespace, derivations }:
      { linkFarm, stdenv }:
      let
        flatten =
          prefix: tree:
          lib.concatMapAttrs (
            name: value:
            let
              key = if prefix == "" then name else "${prefix}.${name}";
            in
            if lib.isDerivation value then { ${key} = value; } else flatten key value
          ) tree;

        flat = flatten "" derivations;

        bundle =
          name: tree:
          linkFarm name (
            lib.mapAttrsToList (name: path: {
              inherit name path;
            }) (flatten "" tree)
          );

        # every level looks like a derivation so that the Nix CLI keeps
        # completing through it
        namespaced =
          path: tree:
          let
            name = lib.concatStringsSep "." path;
          in
          lib.mapAttrs (
            child: value: if lib.isDerivation value then value else namespaced (path ++ [ child ]) value
          ) tree
          // {
            all = bundle name tree;
            inherit name;
            type = "derivation";
            inherit (stdenv.hostPlatform) system;
          };
      in
      {
        packages = {
          ${namespace} = namespaced [ namespace ] derivations;
        }
        // lib.mapAttrs' (name: lib.nameValuePair "${namespace}.${name}") flat;

        legacyPackages = {
          # Tip(debugging): use this when not using the Flake setup (`nix repl -f.`)
          # to get a curated list of packages `pkgs.<Tab>`
          # In the Flake setup, it's equivalent to use `nix flake show`.
          # This is because simply querying `pkgs` will not display the list,
          # `pkgs` being a derivation and not an attrset of derivations
          # also in the Traditional setup to keep consistency between Flake and Traditional.
          "${namespace}Repl" = derivations;
        };
      };

    # Get the Nix store hash of a derivation's output path
    # (eg. `/nix/store/<hash>-name` -> `<hash>`).
    nixStoreHash = drv: lib.unsafeDiscardStringContext (lib.substring 0 32 (baseNameOf drv.outPath));

    # Recursively remove Nix context used to track dependencies.
    # Useful to avoid building the derivations contained in a `config`
    # when serializing it (eg. with `builtins.toJSON`).
    scrubNixContext =
      x:
      if lib.isString x || lib.isDerivation x then
        lib.unsafeDiscardStringContext x
      else if lib.isFunction x then
        null
      else if lib.isList x then
        map config.flake.lib.scrubNixContext x
      else if lib.isAttrs x then
        lib.mapAttrs (n: v: if n == "__toString" then v else config.flake.lib.scrubNixContext v) x
      else
        x;
  };
}
