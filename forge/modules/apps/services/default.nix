{
  options,
  config,
  specialArgs,

  lib,
  ...
}:
{
  options = {
    components = lib.mkOption {
      type = lib.types.attrsOf (
        lib.types.submoduleWith {
          inherit specialArgs;
          modules = [ ./component.nix ];
        }
      );
      default = { };
      description = ''
        Services components.

        Each component must have `process.command` set and can optionally
        declare one or more `resources` providing NixOS configuration.
      '';
      apply =
        self:
        lib.mapAttrs (
          _: service:
          let
            knownComponents = lib.attrNames config.components;
            invalidAfterDeps = lib.filter (dep: !lib.elem dep knownComponents) service.after;
            optionPath = lib.showOption (options.components.loc ++ [ service.name ]);

            prettyPrint = lib.generators.toPretty { };

            checks.after = {
              cond = invalidAfterDeps != [ ];
              msg = ''
                `${optionPath}.after` references invalid services: ${prettyPrint invalidAfterDeps}
                Must be one of: ${prettyPrint knownComponents}
              '';
            };

          in

          assert (lib.any (c: lib.throwIf c.cond c.msg true) (lib.attrValues checks));

          service
          // {
            result = {
              process.argv =
                let
                  serviceCommand =
                    if lib.isDerivation service.process.command then
                      lib.getExe service.process.command
                    else
                      service.process.command;
                in
                [ serviceCommand ] ++ service.process.argv;
              configData = service.process.configData;
              preStart = service.process.preStart;
            };
          }
        ) self;
    };

    runtimes = lib.mkOption {
      type = lib.types.submoduleWith {
        inherit specialArgs;
        modules = [ ./runtimes ];
      };
      default = { };
      description = "Portable services runtimes.";
    };

    resources = lib.mkOption {
      internal = true;
      type = lib.types.attrsOf (
        lib.types.submodule {
          options.nixosConfig = lib.mkOption {
            type = lib.types.deferredModule;
            default = { };
            description = ''
              Runtime independent configuration of the resource.

              See the list of available
              [NixOS options](https://search.nixos.org/options) .
            '';
            example = lib.literalExpression "{ services.postgresql.enable = true; }";
          };
          options.ports = lib.mkOption {
            type = lib.types.listOf (lib.types.strMatching "^[0-9]+:[0-9]+$");
            default = [ ];
            description = ''
              List of ports exposed by the resource.

              Format:
                _HOST_PORT:RESOURCE_PORT_
            '';
            example = lib.literalExpression ''[ "5432:5432" ]'';
            apply = self: lib.unique self;
          };
        }
      );
      default = { };
      description = "Resource configuration";
      example = lib.literalExpression ''
        {
          database.nixosConfig = { services.postgresql.enable = true; };
          cache.nixosConfig = { services.redis.servers.default.enable = true; };
        }
      '';
    };
  };

  config.resources =
    let
      componentResources = lib.pipe config.components [
        (lib.attrValues)
        (lib.catAttrs "resources")
      ];

      runtimeResources = [ config.runtimes.container.resources ];
    in
    lib.mkMerge (runtimeResources ++ componentResources);
}
